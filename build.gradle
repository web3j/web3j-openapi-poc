buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'io.epirus:epirus-gradle-plugin:2.0.0-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:2.1.1'
    }
}

plugins {
    id 'de.undercouch.download' version '4.1.1'
    id "com.diffplug.gradle.spotless" version "3.25.0"
    id 'io.codearte.nexus-staging' version '0.21.1'
    id "de.marcphilipp.nexus-publish" version '0.4.0'
}

description = 'Web3j-openapi project.'

subprojects {
    apply plugin: "kotlin"
    apply from: "$rootProject.projectDir/gradle/versions.gradle"

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    apply {
        [
                'bintray',
                'publish',
                'jacoco',
                'java',
                'javadoc',
                'repositories',
                'spotless'
        ].each { buildScript ->
            download {
                src "https://raw.githubusercontent.com/web3j/build-tools/master/gradle/$buildScript/build.gradle"
                dest "$rootDir/gradle/$buildScript/build.gradle"
                overwrite true
                quiet true
                onlyIfModified true
            }
            from("$rootDir/gradle/$buildScript/build.gradle")
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                pom {
                    scm {
                        url = "https://github.com/web3j/web3j-openapi"
                        connection = "scm:https://github.com/web3j/web3j-openapi.git"
                        developerConnection = "scm:git://github.com/web3j/web3j-openapi.git"
                    }
                }
            }
        }
    }

    tasks.withType(Test) {
        // FIXME To be removed or fix signature issue
//        dependsOn publishToMavenLocal
//        mustRunAfter publishToMavenLocal
        reports.html.destination = file("${reporting.baseDir}/${name}")
        useJUnitPlatform()
    }

    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "io.github.microutils:kotlin-logging:${versions.kotlinLogging}"
        implementation "ch.qos.logback:logback-classic:${versions.logback}"
        implementation "org.slf4j:slf4j-api:${versions.slf4j}"
        implementation "org.slf4j:jul-to-slf4j:${versions.slf4j}"

        testImplementation "com.willowtreeapps.assertk:assertk-jvm:${versions.assertk}"
        testImplementation "org.junit.jupiter:junit-jupiter:${versions.junit}"
        testImplementation "org.mockito:mockito-junit-jupiter:${versions.mockitoJunit}"

        configurations.all {
            resolutionStrategy {
                force(group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: versions.jackson)
                force(group: 'io.github.microutils', name: 'kotlin-logging', version: versions.kotlinLogging)
                force(group: 'io.swagger.core.v3', name: 'swagger-core', version: versions.swagger)
                force(group: 'io.swagger.core.v3', name: 'swagger-models', version: versions.swagger)
                force(group: 'io.swagger.core.v3', name: 'swagger-annotations', version: versions.swagger)
            }
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    sourceCompatibility = 1.8
}
